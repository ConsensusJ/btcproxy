plugins {
    id("groovy")
    id("com.github.johnrengelman.shadow") version "6.1.0"
    id("io.micronaut.application") version "1.3.4"
}

version = btcProxydVersion
group = "org.consensusj.bitcoin.proxyd"

buildScan {
    if (System.getenv('CI')) {
        publishAlways()
        tag 'CI'
    }
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
}

repositories {
    //mavenLocal()
    mavenCentral()
    maven { url 'https://dl.bintray.com/msgilligan/maven' } // consensusj (RPC client) (will move)
    maven { url 'https://dl.bintray.com/consensusj/maven' }
}

micronaut {
    runtime("netty")
    testRuntime("spock2")
    processing {
        incremental(true)
        annotations("org.consensusj.bitcoin.proxyd.*")
    }
}

dependencies {
    compileOnly("org.graalvm.nativeimage:svm")
    implementation("io.micronaut:micronaut-validation")
    implementation("io.micronaut:micronaut-runtime")
    implementation("javax.annotation:javax.annotation-api")
    implementation("io.micronaut:micronaut-http-client")
    implementation("io.micronaut.rxjava3:micronaut-rxjava3")
    runtimeOnly("ch.qos.logback:logback-classic")

    implementation "com.msgilligan:cj-btc-jsonrpc:${consensusjVersion}"

    implementation "foundation.omni:omnij-analytics:${omnijVersion}"
    implementation "foundation.omni:omnij-jsonrpc:${omnijVersion}"
    implementation "foundation.omni:omnij-net-api:${omnijVersion}"
}


application {
    mainClass.set("org.consensusj.bitcoin.proxyd.Application")
}

java {
    sourceCompatibility = JavaVersion.toVersion("11")
    targetCompatibility = JavaVersion.toVersion("11")
}

test {
    // Don't use MICRONAUT_CONFIG_FILES env variable in test
    environment(['MICRONAUT_CONFIG_FILES':''])
}

dockerBuild {
    images = ["${System.env.DOCKER_IMAGE ?: project.name}:$project.version"]
}

dockerBuildNative {
    images = ["${System.env.DOCKER_IMAGE ?: project.name}:$project.version"]
}

